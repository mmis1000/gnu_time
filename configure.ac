dnl Process this file with autoconf to produce a configure script.

AC_INIT([GNU Time],
        [m4_esyscmd([build-aux/git-version-gen .tarball-version])],
        [bug-time@gnu.org], [],
        [https://www.gnu.org/software/time/])

dnl Must come before AM_INIT_AUTOMAKE.
AC_CONFIG_AUX_DIR([build-aux])
AM_INIT_AUTOMAKE([
	1.11.1
	subdir-objects
	dist-xz
	color-tests
	parallel-tests
])

dnl Minimum Autoconf version required.
AC_PREREQ([2.62])


dnl Where to generate output; srcdir location.
AC_CONFIG_HEADERS([config.h:config.in])
AC_CONFIG_SRCDIR([src/time.c])

dnl Checks for programs.
AC_PROG_CC
AC_PROG_CPP
AC_PROG_INSTALL

# Since we use gnulib: gl_EARLY must be called as soon as possible after
# the C compiler is checked.  The others could be later, but we just
# keep everything together.
gl_EARLY
gl_INIT

# Add extra compilation warnings. These will only apply to datamash.c not to
# any gnulib modules). See $(WARN_CFLAGS) in Makefile.am
gl_WARN_ADD([-Wall])
gl_WARN_ADD([-Wextra])
gl_WARN_ADD([-Wformat-security])
gl_WARN_ADD([-Wswitch-enum])
gl_WARN_ADD([-Wswitch-default])
gl_WARN_ADD([-Wformat-nonliteral])
gl_WARN_ADD([-Wunused-parameter])
gl_WARN_ADD([-Wfloat-equal])
gl_WARN_ADD([-fdiagnostics-show-option])
gl_WARN_ADD([-funit-at-a-time])
gl_WARN_ADD([-Wmissing-format-attribute])
gl_WARN_ADD([-Wstrict-overflow])
gl_WARN_ADD([-Wsuggest-attribute=const])
gl_WARN_ADD([-Wsuggest-attribute=pure])
AC_SUBST([WARN_CFLAGS])

dnl Checks for header files.
AC_HEADER_STDC
AC_HEADER_SYS_WAIT

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_PID_T
AC_TYPE_SIZE_T
AC_TYPE_SIGNAL

dnl Checks for library functions.
AC_FUNC_VPRINTF
AC_FUNC_WAIT3
AC_CHECK_FUNCS(strerror)


# What memory units are reported by getrusage(2) ?
case "$host_os" in
  linux*)
     AC_DEFINE([GETRUSAGE_RETURNS_KB],[1],
               [Define to 1 if getrusage(2) on this systems returns
                ru_maxrss in kilobytes])
     ;;

  macos* | darwin*)
     AC_DEFINE([GETRUSAGE_RETURNS_BYTES],[1],
               [Define to 1 if getrusage(2) on this systems returns
                ru_maxrss in bytes])
     ;;

  solaris*)
     AC_DEFINE([GETRUSAGE_RETURNS_PAGES],[1],
               [Define to 1 if getrusage(2) on this systems returns
                ru_maxrss in pages])
     ;;

  *) AC_MSG_ERROR([unknown getrusage behavior on host on '$host_os',
                   please report this to bug-time@gnu.org]);;
esac


# This is needed when building outside the source dir
# with --disable-dependency-tracking.
# Inspired by sed's https://bugs.gnu.org/25371
AS_MKDIR_P([lib])
AS_MKDIR_P([src])
AS_MKDIR_P([doc])
AS_MKDIR_P([tests])

AC_CONFIG_FILES([Makefile])
AC_OUTPUT
